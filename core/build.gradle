apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

repositories {
    jcenter()
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "bloder"
            name "Bloder"
            email "aptgetinstalldanielleme@gmail.com"
        }
    }
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            if (plugins.hasPlugin('war')) {
                from components.web
            } else {
                from components.java
            }
            groupId "com.bloder.shelf"
            artifactId "core"
            version "1.0"
            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'An easy way to implement repository pattern')
                root.appendNode('name', 'shelf')
                root.appendNode('url', 'https://github.com/daniel-martins-ir/Shelf')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = 'Shelf'
        publish = true
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/daniel-martins-ir/Shelf.git'
        publicDownloadNumbers = true
        version {
            name = "1.0"
            desc = 'An easy way to implement repository pattern'
            vcsTag = '1.0.0'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

sourceCompatibility = "1.7"
targetCompatibility = "1.7"
